' Gambas class file

' State

' Initial posX and posY
Private posX As Integer
Private posY As Integer
Private size As Integer
Private arrowColor As Integer

Private arrowPartArray As New ArrowPart[]

' Behaviour

' Builder
Public Sub _new(x As Integer, y As Integer, s As Integer, p As Player)
  
  posX = x
  posY = y
  size = s
  arrowColor = Color.RGB(255, 0, 0)
  
  createArrow(p)
  
End

' Create the first arrow part
Private Sub createArrow(player As Player)
  
  Dim firstPart As ArrowPart
  
  firstPart = New ArrowPart(player.getX() + (player.getWidth() / 2) - (size / 2), player.getY() + (player.getHeight() - size), size)
  
  arrowPartArray.Add(firstPart, 0)
  
End


' Move the arrow to the top of the board
Public Sub Move()
  
  Dim tip As ArrowPart
  Dim newTip As ArrowPart
  
  tip = arrowPartArray[0]

  newTip = New ArrowPart(tip.getX(), tip.getY() - tip.getSize(), tip.getSize())

  arrowPartArray.Add(newTip, 0)
  
End

' Paint arrow (the arrow hitbox will be a circle, at least for now...)
Public Sub paintArrow(board As DrawingArea)
  Dim i As Integer
  For i = 0 To arrowPartArray.Count - 1
    ' Individual job to do for each arrow part
    arrowPartArray[i].paintPart(board)
  Next
  
End

' Check if an arrow tip hit the top border of the board
Public Function hitBorder() As Boolean
  
  Dim tip As ArrowPart
  Dim result As Boolean
  
  tip = arrowPartArray[0]
  
  If (tip.getY() <= 0) Then 
    result = True
  Else 
    result = False
  Endif
  
  Return result
  
End

' Check if the arrow part hit a circle
Public Function hitCircles(circle As Circle) As Boolean
  
  Dim i As Integer
  
  For i = 0 To arrowPartArray.Length - 1
    If ((arrowPartArray[i].getX() < circle.getX() + circle.getDiameter() And 
      arrowPartArray[i].getX() + arrowPartArray[i].getSize() > circle.getX()) And
      (arrowPartArray[i].getY() < circle.getY() + circle.getDiameter() And
      arrowPartArray[i].getY() + arrowPartArray[i].getSize() > circle.getY())) Then
      Return True
    Endif
  Next
  
  
  Return False
  
End

