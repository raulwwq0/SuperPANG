' Gambas class file

' State +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

' PosX: Horizontal position
Private posX As Float

' PosY: Vertical position
Private posY As Float

' Diameter: Diameter in px
Private diameter As Integer

Private gravity As Float
Private speedX As Float
Private speedY As Float

' Set the image
Private circleSprite As Image

' Behaviour +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

' Builder
Public Sub _new(x As Integer, y As Integer, d As Integer)

 posX = x
 posY = y
 diameter = d
 speedX = 3.0
 speedY = 3.0
 gravity = 0.5
 
 ' Load the sprite
 circleSprite = Image.Load("./sprites/circle.png")
End

' Paint a circle
Public Sub paintCircle(board As DrawingArea)
  
  Paint.Begin(board)
  Paint.DrawImage(circleSprite, posX - (diameter / 6), posY, diameter + (diameter / 3), diameter + (diameter / 3))
  Paint.Fill()
  Paint.End
  
End

' Move
Public Sub Move(board As DrawingArea)
  
  posX += speedX
  posY += speedY
  speedY += gravity

  ' Check if the circle hit a lateral wall
  If (posX + diameter + (diameter / 6) > board.Width Or posX - (diameter / 6) < 0) Then
    speedX *= -1
  Endif
  
  
  ' Check if the circle hit the floor
  If (posY + diameter + (diameter / 3) > board.Height) Then
    speedY -= gravity
    
    ' To make the circle bounce less depending on the diameter size I will substract a const to the max speed of a big circle
    If (diameter = 25) Then         ' <- A small circle
      speedY = -(FMain.getMaxCircleSpeedY() - 10)
    Else If (diameter = 50) Then    ' <- A medium circle
      speedY = -(FMain.getMaxCircleSpeedY() - 5)
    Else                            ' <- A big circle
      speedY *= -1
    Endif
    
  Endif
  
End


' Getters
' Get X
Public Function getX() As Integer
  
  Return posX
  
End

' Get Y
Public Function getY() As Integer
  
  Return posY
  
End

'Get side
Public Function getDiameter() As Integer

  Return diameter
  
End

' Get speed in Y
Public Function getSpeedY() As Float
  
  Return speedY
  
End

' Get speed in X
Public Function getSpeedX() As Float
  
  Return speedX
  
End

' Setters
' Set speed in Y
Public Sub setSpeedY(newSpeedY As Float)

  speedY = newSpeedY
  
End

' Set speed in X
Public Sub setSpeedX(newSpeedX As Float)

  speedX = newSpeedX
  
End
